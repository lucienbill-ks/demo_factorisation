//Fichier pour les fonctions publiques de type action
to je_verifie_que_le_SUT_est_disponible
	logFct
	// note : nécessaire pour DAI, qui ouvre/ferme le SUT pour chaque test
	set max_wait_time to 120 // 2 minutes
	set wait_interval to 5
	set max_repeats to round(max_wait_time/wait_interval)
	
	set is_sut_open to false
	repeat max_repeats times
		if imagefound(imagename:"apps/windows/windows_logo_taskbar", waitfor:0)
			set is_sut_open to true
			exit repeat
		end if
		wait wait_interval
	end repeat
	
	if not is_sut_open then throw "Le SUT n'est pas disponiblee"
	
	(*
	if imagefound(imagename:"apps/windows/cross_error_message", waitfor:20) then
		typeText returnkey
		wait 0.5
	end if
	*)
	
	logFct
end je_verifie_que_le_SUT_est_disponible

to j_execute_une_commande_windows_r with myCmd
	logFct myCmd
	TypeText windowsKey, "r"
	wait 0.5
	typeText myCmd, returnKey
	wait 0.5
	logFct
end j_execute_une_commande_windows_r


to je_lance_le_navigateur_x with myBrowser, myOptions:{}
	logFct myBrowser, myOptions
	// initialisation de la liste de navigateurs reconnus
	set browserList to {}

	set chromeName to "chrome"
	// set edgeName to "edge"
	
	set browserList.(chromeName) to "chrome.exe"
	// set browserList.(edgeName) to "msedge.exe"
	
	// Controle des paramètres
	set browserExecName to browserList.(myBrowser) 
	if browserExecName is empty then Throw "Navigateur inconnu", "Le navigateur '"&myBrowser&"' ne fait pas partie de la liste des navigateurs connus " && browserList
	
	// Valeurs par défauts
	if myOptions.startUrl is empty then
		set myOptions.startUrl to "about:blank"
	end if
	if myOptions.startMaximized is empty then
		set myOptions.startMaximized to true
	end if
	if myOptions.incognito is empty then
		set myOptions.incognito to false
	end if
	
	// Chrome
	if myBrowser is chromeName then
		set myCmd to browserList.(myBrowser) && myOptions.startUrl
		if myOptions.startMaximized is true then
			set myCmd to myCmd && "--start-maximized"
		end if
		if myOptions.incognito is true then
			set myCmd to myCmd && "--incognito"
		end if
	end if
	
	j_execute_une_commande_windows_r myCmd
	wait 2
	Attendre "browser/"&myBrowser&"/reloadIcon"
	logFct
end je_lance_le_navigateur_x