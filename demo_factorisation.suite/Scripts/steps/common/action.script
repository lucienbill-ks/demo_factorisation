//File for public functions that are actions, such as "I_do_this"

to I_execute_this_windows_r_command with myCmd
	logFct myCmd
	TypeText windowsKey, "r"
	wait 0.5
	typeText myCmd, returnKey
	wait 0.5
	logFct
end I_execute_this_windows_r_command


to I_launch_browser_x_at_url_y with myBrowser, myOptionalURL:"about:blank"
	logFct myBrowser, myOptionalURL
	
	// I use these as constants, to avoid making any typo. Might be overkill though
	set C_chrome to "chrome"
	set C_chromeIncognito to "chrome_incognito"
	// set edgeName to "edge"
	
	// setting up the list of known browsers
	set browserList to {}
	set browserList.(C_chrome) to "chrome.exe"
	set browserList.(C_chromeIncognito) to "chrome.exe"
	// set browserList.(edgeName) to "msedge.exe"
	
	// Checking the parameters
	set browserExecName to browserList.(myBrowser) 
	if browserExecName is empty then Throw "Unknown", "The browser '"&myBrowser&"' does not belong the list of known browsers:" && browserList
	
	
	// depending on which Browser is selected, build the instruction to run
	
	If myBrowser equals ...
		... C_chrome :
			set myCmd to browserList.(myBrowser) && myOptionalURL && "--start-maximized"
		... C_chromeIncognito :
			set myCmd to browserList.(myBrowser) && myOptionalURL && "--start-maximized" && "--incognito"
	End if
	
	// launch it
	I_execute_this_windows_r_command myCmd
	wait 2
	
	// Wait for it to be fully launched
	do_waitfor "browser/"&myBrowser&"/reloadIcon"
	
	logFct
end I_launch_browser_x_at_url_y

to I_run_all_tests_of_folder_x with myFolder:empty
	if myFolder is empty then Throw "Missing parameter", "parameter 'myFolder' is mandatory"
	
	set scriptDirPath to the resolvedfilePath of ( ResourcePath() & "/../Scripts/" & myFolder)
	
	set listOfTestsToRun to files of scriptDirPath
	
	if the number of items of listOfTestsToRun is 0 then
		Throw "No tests to run", "No script found in" && myFolder
		
	end if
	
	Repeat with each item myTest of listOfTestsToRun
		RunWithNewResults myFolder & "/" & myTest
		
		// if the result's status is "failure" then
		// LogError "test failed. Performing cleanup action"
		// some actions
		// end if	
	End repeat
	
end I_run_all_tests_of_folder_x