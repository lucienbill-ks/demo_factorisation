//File for public functions that are actions, such as "I_do_this"

to I_create_a_new_file
	logFct
	wait 3
	MoveTo text:"en-US", waitFor:30
	do_click "apps/vscode_online/create_new_file"
	wait 2
	
	// Note: I use a "getter" here. I could use global variable, but I'd rather have
	// a "getter/setter" system, which works well with autocompletion and
	// will fail if I make a typo (which makes debugging easier)
	set filename to "steps/vscode_online/_lib"._get_default_newfile_name()
	
	typeText filename, returnkey
	wait 2
	typeText "this is a new file!", returnkey
	WaitFor 4, text:"this is a new file!"
	logFct	
end I_create_a_new_file

to I_delete_the_new_file
	logFct
	set filename to "steps/vscode_online/_lib"._get_default_newfile_name()
	
	// do_RightClick filename, SearchRectangle:[0,92,350, RemoteScreenSize().y]
	
	log "Right click on the '" & filename & "' text that is the closest to the left of the screen"
	set myList to EveryImageLocation(text:filename)
	set ref_coord to RemoteScreenSize()
	Repeat with each item myCoord of myList
		// keep the one that is more on the left
		if myCoord.x is less than ref_coord
			set ref_coord to myCoord
		end if
	End repeat
	
	RightClick ref_coord
	
	// Delete the file
	wait 1
	do_click "Delete Permanently"
	wait 1
	do_waitfor "Are you sure you want to permanently delete"
	typeText returnkey	
	logFct
end I_delete_the_new_file

to I_close_VS_Code_online
	logFct
	typeText controlKey, "w"
	
	wait 0.5
	if ImageFound(text:"Leave site?", waitFor:4) then
		typeText returnKey
	end if
	
	set isExitOK to false
	Repeat 5 times
		if not ImageFound(imageName:"apps/vscode_online/topleft_logo.png", waitFor:0) then
			set isExitOK to true
			exit repeat
		end if
		
		wait 2
	End repeat
	
	if not isExitOK then Throw "Failed to quit VS Code Online", "It's still opened"
	logFct
end I_close_VS_Code_online