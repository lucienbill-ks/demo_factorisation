//Fichier pour les fonctions publiques de type action

to je_vais_sur_github_sans_me_connecter_a_un_compte
	logFct
	"steps/github/_lib"._ensure_disconnection {nouvelleSessionNavigateur:true}
	logFct
end je_vais_sur_github_sans_me_connecter_a_un_compte

je_vais_sur_github_sans_me_connecter_a_un_compte

to je_vais_sur_le_repo_de_test with repo_url:"steps/github/_lib"._get_default_test_repo()
	logFct repo_url
	
	wait 1
	TypeText f6 // revient à cliquer dans la barre d'URL
	wait 0.5
	typeText repo_url, returnkey
	wait 2
	
	set validationStr to repo_url
	delete "https://github.com/" from validationStr
	
	waitfor 10, text:validationStr, SearchRectangle: [0,93,RemoteScreenSize().x, 300]
	
	logFct
end je_vais_sur_le_repo_de_test

to je_tente_d_ouvrir_vs_code_online
	logFct
	typeText "."
	wait 2
	logFct
end je_tente_d_ouvrir_vs_code_online

to je_m_authentifie_a_github
	logFct
	cliquer "Sign in"
	wait 1
	WaitFor 10, text:"Username or email address", SearchRectangle:empty
	
	"steps/keepass/action".j_ouvre_le_coffree_keepass
	"steps/keepass/action".j_utilise_autotype
	WaitFor 10, text:"Two-factor authentication"
	"steps/keepass/action".je_copie_colle_le_code_otp_de_l_entree_selectionnee
	wait 5
	WaitFor 30, text:"Pull requests"
	if ImageFound(text:"Sign in", waitFor:2) then
		Throw "Echec du login à GitHub", "L'authentification n'a pas fonctionné"
	end if
	
	logFct
end je_m_authentifie_a_github

to je_me_deconnecte_de_github
	logFct
	"steps/github/_lib"._ensure_disconnection {nouvelleSessionNavigateur:false}
	logFct
end je_me_deconnecte_de_github