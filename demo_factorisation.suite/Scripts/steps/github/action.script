//File for public functions that are actions, such as "I_do_this"

to I_go_to_github_and_make_sure_I_am_disconnected
	logFct
	"steps/github/_lib"._ensure_disconnection
	logFct
end I_go_to_github_and_make_sure_I_am_disconnected

to I_go_to_the_test_repo with repo_url:empty	
	logFct repo_url
	
	if repo_url is empty
		set repo_url to "steps/github/_lib"._get_default_test_repo()
	end if
	
	wait 1
	TypeText f6 // this is like clicking on the URL bar
	wait 0.5
	typeText repo_url, returnkey
	wait 2
	
	set validationStr to repo_url
	delete "https://github.com/" from validationStr
	
	waitfor 30, text:validationStr, SearchRectangle: [0,93,RemoteScreenSize().x, 300]

	logFct
end I_go_to_the_test_repo

to I_attempt_to_open_vs_code_online
	logFct
	typeText "."
	wait 2
	logFct
end I_attempt_to_open_vs_code_online

to I_log_into_github
	logFct
	do_click "Sign in"
	wait 1
	WaitFor 10, text:"Username or email address", SearchRectangle:empty
	
	"steps/keepass/action".I_open_the_keepass_vault
	"steps/keepass/action".I_use_the_autotype_for_the_credentials
	WaitFor 10, text:"Two-factor authentication"
	"steps/keepass/action".I_copy_paste_the_OTP_code_of_the_current_entry
	wait 5
	WaitFor 30, text:"Pull requests"
	if ImageFound(text:"Sign in", waitFor:2) then
		Throw "Echec du login à GitHub", "L'authentification n'a pas fonctionné"
	end if
	
	logFct
end I_log_into_github

to I_disconnect_from_github
	logFct
	"steps/github/_lib"._ensure_disconnection
	logFct
end I_disconnect_from_github